plugins {
    id 'maven-publish'
    id 'com.jfrog.artifactory'
}
ext {
    String releaseBranches = project.findProperty("GIT_RELEASE_BRANCHES") ?: "origin/master,master"
    branchName = (project.findProperty("GIT_BRANCH") ?: System.env.GIT_BRANCH) ?: "undefined"
    isReleaseBranch = releaseBranches.split(',').contains(branchName)
    gitUrl = (project.findProperty("GIT_URL") ?: System.env.GIT_URL) ?: "https://localhost/UNDEFINED_SPK/UNDEFINED_REPO.git"
    buildUrl = project.findProperty("BUILD_URL") ?: System.env.BUILD_URL
    gitCommit = project.findProperty("GIT_COMMIT") ?: System.env.GIT_COMMIT
    def matcher = (gitUrl =~ ".*?(?<spk>[\\w_-]+)/(?<repo>[\\w_-]+)\\.git")
    if(matcher.matches()) {
        spk = matcher.group("spk")
        repo = matcher.group("repo")
    }
    artifactoryRepoName = isReleaseBranch ? "releases" : "snapshots"
    artifactory_contextUrl = project.findProperty("artifactory_contextUrl") ?: "https://artifactory.org.com/artifactory"
    artifactory_user = project.findProperty("artifactory_user") ?: System.getProperty("gradle.wrapperUser")
    artifactory_password = project.findProperty("artifactory_password") ?: System.getProperty("gradle.wrapperPassword")
}

def gitBranch() {
    gitCmd("git rev-parse --abbrev-ref HEAD")
}

def gitUrlFromGit() {
    gitCmd("git ls-remote --get-url origin")
}

def gitCmd(String cmd) {
    def result = ""
    def proc = cmd.execute()
    try {
        proc.in.eachLine { line -> result = line }
        proc.err.eachLine { line -> println line }
        proc.waitFor()
        return result
    } catch (Throwable t) {
        return "NA"
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        mavenJava(MavenPublication) { p ->
            versionMapping {
                allVariants {
                    fromResolutionResult()
                }
            }
            afterEvaluate {
                from components.java
                if(project.ext.has("artifactId"))
                    artifactId = project.artifactId
                if(project.hasProperty("artifactId"))
                    groupId = project.getProperty("artifactId")
            }
            pom {
                scm {
                    connection = "$gitUrl"
                    developerConnection = "$gitUrl"
                    url = "$buildUrl"
                    tag = "$gitCommit"
                }
            }
        }
    }
}

artifactory {
    contextUrl = artifactory_contextUrl
    publish {
        repository {
            repoKey = artifactoryRepoName
            username = artifactory_user
            password = artifactory_password
        }
        defaults {
            publications('maven', 'mavenJava', 'ivyJava')
            publishArtifacts = true
            publishPom = true
        }
    }
}

def warnIfPropertyIsNull(propName) {
    if(project.property(propName) == null) {
        logger.lifecycle("Property $propName is null. Please set it in the build script.")
    }
}

tasks.register("beforeArtifactoryPublish") {
    dependsOn("publishingSetup")
    doLast {
        warnIfPropertyIsNull("artifactory_contextUrl")
        warnIfPropertyIsNull("artifactory_user")
        warnIfPropertyIsNull("artifactory_password")
        logger.lifecycle("Publishing (group: ${project.group} to a url: ${artifactory_contextUrl}/${artifactoryRepoName}/)")
    }
}

tasks.named("artifactoryPublish") {
    dependsOn("beforeArtifactoryPublish")
}